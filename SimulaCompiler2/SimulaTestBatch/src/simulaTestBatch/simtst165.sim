
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 165
%
%  PURPOSE:     Test Activate direct
%
%  METHOD:      Try it and see if it work as expected.
%
%  WRITTEN BY:  Dag Langemyhr and MYH
% ==============================================================================
external class SimulaTest;

SimulaTest(165,"--- Test Activate direct") begin
	text array facit(0:17);
	integer traceCase;
   
	procedure trace(msg); text msg; begin
		if verbose then
		begin outtext(msg); outimage; end;
		if msg ne facit(traceCase) then begin
			err("Case " & edit(traceCase));
			outtext("Trace: """ & msg & """"); outimage;
			outtext("Facit: """ & facit(traceCase) & """"); outimage;
		end;
		traceCase:=traceCase+1;
	end;

	
% ========================================================
	
	facit(0) :- "At START";
	
	facit(1) :- "P1: ap er passiv og bp er passiv";
	facit(2) :- "A1: ap er aktiv og bp er passiv";
	facit(3) :- "B1: ap er suspendert og bp er aktiv";
	facit(4) :- "A2: ap er aktiv og bp er suspendert";
	facit(5) :- "B2: ap er terminert og bp er aktiv";
	facit(6) :- "P2: ap er terminert og bp er terminert";
	facit(7) :- "P3: ap er terminert og bp er terminert";
	
	facit(8) :- "At END";
	
% ========================================================

		Simulation begin
		   text procedure status (p);
		   ref(Process) p;
		   begin
		      status :- if p.terminated then "terminert" else
		         if p.idle then "passiv" else
		         if p == current then "aktiv" else "suspendert";
		   end status;
		
		   procedure vis_status (id);
		   text id;
		   begin
		      trace(id & ": ap er " & status(ap) & " og bp er " & status(bp));
		   end vis_status;
		
		   Process class A;
		   begin
		      vis_status("A1");
		      activate bp;
		      vis_status("A2");
		   end A;
		
		   Process class B;
		   begin
		      vis_status("B1");
		      reactivate ap;
		      vis_status("B2");
		   end B;
		
		   ref(A) ap;
		   ref(B) bp;
			trace("At START");
		   ap :- new A;   bp :- new B;
		   vis_status("P1");
		   activate ap;
		   vis_status("P2");
		   hold(100);
		   vis_status("P3");
			trace("At END");
		end



 end