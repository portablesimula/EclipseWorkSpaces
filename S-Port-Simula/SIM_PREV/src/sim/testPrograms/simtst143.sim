
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 143
%
%  PURPOSE:     Simple test of utility procedure accum.
%
%  WRITTEN BY:  MYH
% ==============================================================================
external class SimulaTest;

SimulaTest(143,"--- Simple test of utility procedure accum.") begin

Simulation begin
%	real time;
	long real A1, B1, C1, D1;
	long real A0, B0, C0, D0;

	text procedure edLReal(r); long real r; begin
   		text t;
   		t:-blanks(19); 
   		t.putreal(r, 3);
   		t :- trim(t);
   		edLReal :- t;
	end;
   
	procedure accum0(a,b,c,d); name a,b,c; long real a,b,c,d;
	begin
		a:= a + c * (time-b);  b:= time;  c:= c + d
	end accum0;
	
	procedure test(a,b,c,d); long real a,b,c,d; begin
		integer i;
		A0 := A1 := a;
		B0 := B1 := b;
		C0 := C1 := c;
		D0 := D1 := d;
		for i := 1 step 1 until 5 do begin
			hold(8);
			accum(A1, B1, C1, D1);
			accum0(A0, B0, C0, D0);
			if A0 ne A1 then err("A0=" & edLREAL(A0) & " while A1=" & edLREAL(A1));
			if B0 ne B1 then err("B0=" & edLREAL(B0) & " while B1=" & edLREAL(B1));
			if C0 ne C1 then err("C0=" & edLREAL(C0) & " while C1=" & edLREAL(C1));
			if D0 ne D1 then err("D0=" & edLREAL(D0) & " while D1=" & edLREAL(D1));
		end;
	end;
	
	test(14,34,21,3);	
	
end;
	
end