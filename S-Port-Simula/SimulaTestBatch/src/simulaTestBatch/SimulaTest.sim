 
class SimulaTest(testCase,title); integer testCase; text title;
	hidden protected found_error;
begin
%	integer testCase = 0;
%	text title = "--- XXXXXXXX";

	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;
     
	text procedure trim(t); text t; begin
   		character c;
   		t :- t.strip;
   		t.setpos(1);
   		c := t.getchar;
   		while c = ' ' do begin
   			t :- t.sub(2,t.length-1);
	   		t.setpos(1);
   			c := t.getchar;
   		end;
   		trim :- t;
	end;
   
	text procedure edInt(i); integer i; begin
   		text t;
   		t:-blanks(19); 
   		t.putint(i);
   		t :- trim(t);
   		edInt :- t;
	end;
   
   text procedure edBool(b); boolean b; begin
   		edBool :- (if b then "true" else "false");
   end;

	text procedure edChar(c); character c; begin
   		text t;
   		t:-blanks(1); 
   		t.putchar(c);
   		edChar :- t;
	end;

	text procedure edFix(r, i); real r; integer i; begin
   		text t;
   		t:-blanks(19); 
   		t.putfix(r, i);
   		t :- trim(t);
   		edFix :- t;
	end;
   
	text procedure edReal(r); real r; begin
   		text t;
   		t:-blanks(19); 
   		t.putreal(r, 3);
   		t :- trim(t);
   		edReal :- t;
	end;
   
	text procedure edLReal(r); long real r; begin
   		text t;
   		t:-blanks(19); 
   		t.putreal(r, 3);
   		t :- trim(t);
   		edLReal :- t;
	end;


	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;




	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(testCase,4); outimage;
		outtext (title);
		outimage; outimage;
	end;
	
	
	
	inner;
	
	
	
	if noMessage then else begin
		if found_error then begin
			outtext("--- "); outint(nFailed,3); outtext(" ERROR(S) FOUND IN TEST"); outint(testCase,4); outtext("  "); outtext(title);
			outtext("Test sample has "); outint(nFailed,3);
			error(" error(s)");
		end else begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(testCase,4); outtext("  "); outtext(title);
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(testCase,4); outimage;
		end;
	end;
	
end;
