
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 101
%
%  PURPOSE:     Test Standard Procedure 'sourceline'.
%
%  WRITTEN BY:  MYH   30/07-2018
% ==============================================================================
external class SimulaTest;

SimulaTest(101,"--- Test Standard Procedure 'sourceline'.") begin

   ref(A) x;
   integer traceCase;
   text array facit(0:3);
   
   procedure trace(line,msg); integer line; text msg;
   begin
      text case1;
      case1:-"LINE " & edint(line) & ": " & msg;
      if verbose then begin outtext(case1); outimage; end;
      if case1 <> facit(traceCase) then
      begin
         err("Trace Case " & edInt(traceCase) & " at line " & edInt(line));
         outtext("Trace Case: " & case1); outimage;
         outtext("     Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;
   
   procedure Q(line); integer line;
   begin
         trace(line,"Call Q");
   end;

   class A(line); integer line;
                  virtual: procedure P;
   begin integer i;
      procedure P(line); integer line;
      begin
            trace(line,"Call P");
      end;
      trace(line,"NEW A");
      i:=sourceline;
      trace(i+1,"end A");
   end;
   
   facit(0):-"LINE 53: NEW A";
   facit(1):-"LINE 45: end A";
   facit(2):-"LINE 54: Call P";
   facit(3):-"LINE 55: Call Q";
   
   x:-new A(sourceline);
   x.P(sourceline);
   Q(sourceline);

end